# Maintainer: Bhanupong Petchlert <bpetlert@gmail.com>
pkgname=xhci_hcd-rebind
pkgver=0.1.0
pkgrel=1
pkgdesc="A serverice for monitor xhci_hcd bus, restart if bus failure"
arch=('x86_64')
url="https://github.com/bpetlert/xhci_hcd-rebind"
license=('MIT')
depends=('systemd')
makedepends=(cargo)
options=(!lto)
provides=("${pkgname}")
conflicts=("${pkgname}")

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bpetlert/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build  --frozen --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/xhci_hcd-rebind" "$pkgdir/usr/bin/xhci_hcd-rebind"

  install -Dm644 "xhci_hcd-rebind.service" "$pkgdir/usr/lib/systemd/system/xhci_hcd-rebind.service"

  install -Dm644 "xhci_hcd-rebind.conf" "$pkgdir/etc/systemd/xhci_hcd-rebind.conf"

  install -Dm644 "README.adoc" "$pkgdir/usr/share/doc/${pkgname}/README.adoc"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
