# Maintainer: Bhanupong Petchlert <bpetlert@gmail.com>
pkgname=xhci_hcd-rebind
pkgver=0.2.0
pkgrel=1
pkgdesc="A service for monitoring xhci_hcd bus, restart if bus failure"
arch=('x86_64')
url="https://github.com/bpetlert/xhci_hcd-rebind"
license=('GPL-3.0-or-later')
depends=('systemd')
makedepends=(cargo)
options=(!lto)
provides=("${pkgname}")
conflicts=("${pkgname}")
backup=(etc/systemd/xhci_hcd-rebind.toml)

# Build from local directory
source=()

# Using the most recent annotated tag reachable from the last commit.
pkgver() {
  cd "${startdir}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${startdir}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${startdir}"

  export RUSTUP_TOOLCHAIN=stable

  # Ignore target-dir in ~/.cargo/config, reset it to default: "target".
  # Add "$srcdir" to support BUILDDIR variable in makepkg.
  export CARGO_TARGET_DIR=$srcdir/target

  cargo build  --frozen --release
}

package() {
  cd "$srcdir"
  install -Dm755 "target/release/xhci_hcd-rebind" "$pkgdir/usr/bin/xhci_hcd-rebind"

  install -Dm644 "${startdir}/xhci_hcd-rebind.service" "$pkgdir/usr/lib/systemd/system/xhci_hcd-rebind.service"

  install -Dm644 "${startdir}/xhci_hcd-rebind.toml" "$pkgdir/etc/systemd/xhci_hcd-rebind.toml"

  install -Dm644 "${startdir}/README.adoc" "$pkgdir/usr/share/doc/${pkgname}/README.adoc"
  install -Dm644 "${startdir}/COPYING" "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
